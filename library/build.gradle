plugins {
    id 'com.android.library'
    id 'maven-publish'
}

ext {
    // Maven metadata
    publishedGroupId = 'com.romanpulov.library.gdrive'
    libraryName = 'library'
    // Save yourself a head ache, and set this equal to the name of the Android Studio library
    // module. The artifact name needs to match the name of the library.
    artifact = 'library'

    libraryDescription = 'Roman Pulov GDrive library for Android'
    libraryVersion = '0.0.12'
}

android {
    namespace = "com.romanpulov.library.gdrive"

    defaultConfig {
        compileSdk 34
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
        }
        singleVariant('debug') {
            withSourcesJar()
        }
    }

}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.android.volley:volley:1.2.1'
    api 'com.google.android.gms:play-services-auth:20.7.0'

    implementation 'com.romanpulov:jutils-core:0.4.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = publishedGroupId
                artifactId = 'library-debug'
                version = libraryVersion
            }
        }
    }
}